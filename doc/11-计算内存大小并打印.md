# 计算内存大小并打印

​	距离我们上次拿到内存信息已经过了许久，我们现在就去使用它们，计算我们的内存大小并打印出来！但在那之前，我们要开始了解汇编函数如何编写才可以和C语言互相调用的标准了，我们接下来的函数将要遵循标准，为我们接下来内核中汇编与C的联用做好准备，同时也是因为这一节编写的代码可以在内核中复用哦～

​	汇编和C语言混合编程时，有一套严格的标准，GCC等编译器也是通过这个标准来设计的，我简单来概述一下：

​	一个C语言函数，例如：int add(int num1, int num2);

那么它被编译成汇编代码将是以下的结构：

```assembly	
add:
		mov eax, [esp + 4]	; 拿到num1的值
	 	mov ebx, [esp + 8]	; 拿到num2的值
	 	
	 	; 进行一通处理
	 	
	 	mov eax, 计算结果		; 返回值 
		ret
```

​	大家不用在意细节，不同的编译器编译成的汇编代码可能不同，但是原理是相同的，标准规定，汇编函数要想和C语言互相调用，调用的时候从最后一个参数开始压栈，而C语言函数将像上面一样，从栈顶往下开始拿参数，这样就可以从第一个参数开始拿了！

​	如果函数有返回值的话，小于32位就存放在eax中，大于32位则一般存在堆栈中。而如果要调用函数，则应该也要遵循以上的标准，例如我们要使用汇编调用以上的函数，则：

```assembly
	push eax		; num2的值
	push ebx 		; num1的值
	call add
	; 记住，返回值在eax中
	add esp, 8		; 清理堆栈
```

​	可以看到，我们调用前，参数要从最后一个压栈，然后再调用函数。有一行我们要值得注意！调用完成后，清理堆栈的工作留给了调用着，也就是将栈顶的值恢复到调用函数前，这也是标准规定好的。

​	我们有了这些知识，以后我们的汇编和C语言就可以其乐融融的一起配合工作了，只要我们编写的汇编函数以及调用函数时遵循以上的标准！

```assembly
;============================================================================
;   显示 AL 中的数字
;----------------------------------------------------------------------------
PrintAl:
	push ecx
	push edx
	push edi
	push eax

	mov edi, [ddDispPos]	; 得到显示位置

	mov ah, 0Fh		; 0000b: 黑底	1111b: 白字
	mov dl, al
	shr al, 4
	mov ecx, 2
.begin:
	and al, 01111b
	cmp al, 9
	ja	.1
	add al, '0'
	jmp	.2
.1:
	sub al, 10
	add al, 'A'
.2:
	mov [gs:edi], ax
	add edi, 2

	mov al, dl
	loop .begin

	mov [ddDispPos], edi	; 显示完毕后，设置新的显示位置

    pop eax
	pop edi
	pop edx
	pop ecx

	ret
```



```assembly
;============================================================================
;   显示一个整形数
;----------------------------------------------------------------------------
PrintInt:
    mov	ah, 0Fh			; 0000b: 黑底    1111b: 白字
    mov	al, '0'
    push	edi
    mov	edi, [ddDispPos]
    mov	[gs:edi], ax
    add edi, 2
    mov	al, 'x'
    mov	[gs:edi], ax
    add	edi, 2
    mov	[ddDispPos], edi	; 显示完毕后，设置新的显示位置
    pop edi

	mov	eax, [esp + 4]
	shr	eax, 24
	call	PrintAl

	mov	eax, [esp + 4]
	shr	eax, 16
	call	PrintAl

	mov	eax, [esp + 4]
	shr	eax, 8
	call	PrintAl

	mov	eax, [esp + 4]
	call	PrintAl

	ret
```

















